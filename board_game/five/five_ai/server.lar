import time, net, util;

!<<

import (
    "encoding/json"
)

!>>

void log(String msg)
{
    println("[%s] %s".(time.format_time("2006-01-02 15:04:05", time.time()), msg));
}

class InvalidReq
{
    EmptyException e usemethod;
}

void work(net.TcpConn conn)
{
    defer conn.close();

    var start_time = time.time();

    defer
    {
        var c = catch<InvalidReq>();
        if (c != nil)
        {
            log("invalid req");
            log(c.traceback());
        }
        else
        {
            log("time cost: %.2f sec".(time.time() - start_time));
        }
    }

    log("start calc");

    var req = new util.Vector<char>(nil);
    for (;;)
    {
        var buf = new char[10000];
        long sz = conn.read(buf);
        if (sz == 0)
        {
            break;
        }
        req.extend(buf[: sz].iter());
    }
    var req_arr = req.to_array();

    long board_size = 0;
    !<<
    var board [][]uint8
    err := json.Unmarshal(l_req_arr.arr, &board)
    if err == nil {
        l_board_size = int64(len(board))
    }
    !>>

    if (board_size <= 0)
    {
        throw(new InvalidReq());
    }

    var board = new char[board_size][board_size];
    for (long i = 0; i < board_size; ++ i)
    {
        var row = board[i];
        bool ok = false;
        !<<
        if int64(copy(l_row.arr, board[l_i])) == l_board_size {
            l_ok = true;
        }
        !>>
        if (!ok)
        {
            throw(new InvalidReq());
        }
    }

    var pos = ai_choice(board);

    log("%s".(pos));
    conn.write("[%s, %s]".(pos.row, pos.col).to_char_array());
}

public void main()
{
    net.TcpListener listener = new net.TcpListener(":9999");
    defer listener.close();

    log("start");

    init_ai_model();
    log("ai model inited");

    log("server start");
    while (true)
    {
        work(listener.accept());
    }
}
